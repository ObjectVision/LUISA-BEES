container LCC: url = "=LCC/FactSheetName"
{
	parameter<String> FactSheetName:
	[
		'%sourcedataprojdir%/Indicators/Land cover connectivity.pdf' 
	];
	unit<Float32> FLEURO: Expr = "BaseUnit('FLEURO', Float32)", cdf = "FLEURO/k30/ClassBreaks"
	{
		unit<UInt8> k30: NrOfRows = 30
		{
			attribute<UInt32> BrushColor: DialogType = "BrushColor",
			[
				16777215, 
				65535, 1310699, 2621399, 3866564, 5177264, 6487964, 7733129, 9043829, 10288994, 11599694, 12910394, 14155559, 15466259, 16776960,
				0, 18, 36, 54, 72, 91, 109, 127, 145, 163, 182, 200, 218, 236, 255
			];
			attribute<FLEURO> ClassBreaks: DialogType = "Classification",
			[
				0, 0.1, 1.20711, 1.41421, 2.20711, 2.62132, 3.41421, 4.03553, 5, 5.62132, 6, 6.62132, 7.24264, 8.03553, 8.86396, 9.69239, 
				10.864, 12.0355, 13.2782, 14.5208, 16.0355, 17.6569, 20.0355, 22.4142, 24.9706, 28.8345, 33.1777, 39.3137, 49.6274, 73.0772
			];
		}	
	}
	unit<Float32> per_km2: Expr = "1 / km2";
	unit<Float32> FLEURO_km: Expr = "FLEURO / km";
	unit<Float32> FLEURO_km2: Expr = "FLEURO / km2";
	
	unit<UInt8> ConnectivityClass: NrOfRows = 7
	{
		attribute<FLEURO_km> Friction: [10, 4, 1, 0, 2, 4, 4];
		attribute<String>  Label: 
		[
			'Urban',
			'ArableAndCrops',
			'PastureAndIrrigatedArabeleAndAbandoned',
			'DenselyVegetatedNature',
			'SparselyVegetatedAreas',
			'Water',
			'n.a.'
		];
	}
	attribute<ConnectivityClass> ReclassTable(Classifications/LU/Ruralis18):
	[
		0, 1, 2, 3, 3, 4, 2, 2, 1, 1, 3, 4, 4, 4, 5, 3, 2, 2
	];
	unit<SPoint> km15FocalPointMatr: Expr = "range(spoint, point(-int16(15), -int16(15)), point(int16(16), int16(16)))"
	{
		attribute<Bool> InCircle: Expr = "dist2(point(Int16(0), Int16(0), .), UInt32)  <= 225";
		attribute<Float32> RelWeight: Expr = "Float32(InCircle) / sum(Float32(InCircle))";
	}
	
	
	container Templ: IsTemplate = "True"
	{
		unit<WPoint> Domain_Grid;	
		attribute<Classifications/LU/Ruralis18> LU(Domain_Grid);
		
		attribute<ConnectivityClass> CC(Domain_Grid): Expr = "ReclassTable[LU]";	
		attribute<FLEURO_km> Friction(Domain_Grid): Expr = "ConnectivityClass/Friction[MakeDefined(CC, UInt8(6))]";
		unit<UInt32> Patches: Expr = "district(CC)";
		attribute<km2>  veg_opp(Domain_Grid): Expr = "value(cc == value(3, ConnectivityClass) ? pcount(Patches/Districts)[Patches/Districts] : 0, km2)";
		attribute<bool> is_large_veg(Domain_Grid): Expr = "veg_opp > value(25, km2)";
		unit<UInt32> large_veg_sel: Expr = "Subset(is_large_veg)"
		{
			attribute<Domain_Grid> nr_OrgEntity;
		}
		attribute<FLEURO> Cost(Domain_Grid): Expr = "griddist(Friction * value(1, km), large_veg_sel/nr_OrgEntity, const(0, large_veg_sel, FLEURO))",
			Descr = "Total friction of least friction path to a vegetation patch";
			
		attribute<FLEURO>   PatchCost(Domain_Grid): Expr = "(veg_opp > value(0, km2)) ? Cost : Float32(0 / 0) ";
		attribute<per_km2>  PatchContraWeight(Domain_Grid): Expr = "veg_opp > value(0, km2) ? Float32(1) / Float32(veg_opp) : Float32(0)";
		attribute<FLEURO_km2> WeightedPatchCost(Domain_Grid): Expr = "Cost * PatchContraWeight";
		attribute<per_km2>  PatchContraWeightPotential(Domain_Grid): Expr = "potential(PatchContraWeight, km15FocalPointMatr/RelWeight)";
		attribute<FLEURO_km2>  WeightedPatchCostPotential(Domain_Grid): Expr = "potential(WeightedPatchCost, km15FocalPointMatr/RelWeight)";
		
		attribute<FLEURO> AverageCostPerCell(Domain_Grid): Expr = "WeightedPatchCostPotential / PatchContraWeightPotential";
		attribute<FLEURO> AverageCostPerNuts2(Geography/RegionShapes/Nuts2): Expr = "sum(WeightedPatchCost, Geography/RegionGrids/AggrRel/Nuts2[AlbersGridRel]) / sum(PatchContraWeight, Geography/RegionGrids/AggrRel/Nuts2[AlbersGridRel])";
		attribute<FLEURO> AverageCostPerNuts0(Geography/RegionShapes/Nuts0): Expr = "sum(WeightedPatchCost, Geography/RegionGrids/AggrRel/Nuts0[AlbersGridRel]) / sum(PatchContraWeight, Geography/RegionGrids/AggrRel/Nuts0[AlbersGridRel])";
	}
	
}	
