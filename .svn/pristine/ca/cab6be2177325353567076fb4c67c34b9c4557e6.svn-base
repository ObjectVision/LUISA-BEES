container JrcClueWrap:
	Descr =
		"This CalculationSchema allocates according to the description in the article:\n"
		"Hilferink, M.Th.A. & P. Rietveld, The Land Use Scanner;\n"
		"An integrated model for long term projections of land use in urban and rural areas,\n"
		"Journal of Geographic Systems, p. 155-177, 1999.\n"
		"The symbols used in the description are explained in this article and\n"
		"the description of the calculation rules refers to the formula numbers there.",
	IsTemplate = "True",
	ViewAction = "Map View.Thematic:results/landuse/Dominant_gg9"
{
	container RunParameters;
	unit<wpoint> domain_grid;
	
	// ===================== End of Required Case Parameters
	
	unit<uint32> domain: Expr = "CaseData/Land/FreeLandCells";
	
	container ParamSet: Expr = "RunParameters/ParamSet";
	
 	container SuitabilityGenerator: Expr = "='ModelTraits/'+RunParameters/SuitabilityTraits";
 	
	unit<uint16> Periods:    Expr = "Parameters/PeriodSet/result";
	
	//container Neighbourhood: Expr = "Rules/Neighbourhood";
	container DemandSet:     Expr = "RunParameters/DemandSet";
	
	// ===================== End of optional Case Parameters

	// ===================== start of domain relations

//	attribute<Geography/Albers1kmGrid> AlbersGridRel(domain_grid): 		Expr = "CaseData/JrcFactorData/AlbersGridRel";
//	attribute<Geography/Albers1kmGrid> AlbersRel(domain): 				Expr = "AlbersGridRel[domain/grid_rel]"; 
	attribute<Geography/Lambert100mGrid> Lambert100m_rel (domain_grid): Expr = "id(domain_grid)[Geography/Lambert100mGrid]";
	
	unit<uint8> ModelType: Expr = "Classifications/LU/ModelType";
	
	unit<UInt32> 	StaticFactors: 	Expr = "MetaData/StaticFactors";
	unit<UInt32> 	DynamicFactors: 	Expr = "MetaData/DynamicFactors";
	unit<uint8> 	DC:				Expr = "MetaData/DemandCharacteristics";  // demand characteristics
	container Rules: Expr = "RunParameters/AllocSet/Rules";	
	
	parameter<Geography/Regions/Runs> JrcRegionId: expr = "rlookup(RunParameters/Name, Geography/Regions/Runs/Name)";
		
	#include<inRunRegions.dms>
	
	container CaseData
	{
	
		// ============ factor data
		container PrepareVariables {
					attribute<Classifications/LU/Corine> CorineGrid (domain_grid): expr = "rlookup(LoadSpatialData/LandUse_2012/inFile/ReadData, Classifications/Lu/Corine/CLC12_Code)";
					attribute<Classifications/LU/ModelType> LandUseGrid (domain_grid): expr = "Classifications/LU/Corine/MT[CorineGrid]";
					attribute<Classifications/LU/ModelType> landuse (domain): expr = "LandUseGrid[domain/grid_rel]";
			container LoadSpatialData: 	expr = "for_each_ne(MetaData/DataManagement/SpatialDataToLoad/Name, 'Templates/loadDataFile_'+MetaData/DataManagement/SpatialDataToLoad/Type+'('+Quote(MetaData/DataManagement/SpatialDataToLoad/Path)+', domain_grid)')";
			container Variables:		expr = "for_each_nedv(MetaData/DynamicFactorDefinitions/DynamicFactors/Name, MetaData/DynamicFactorDefinitions/DynamicFactors/StartExpr, domain, float32)";
		}
		container JrcFactorData: Expr = "JrcFactorDataTempl(domain_grid, RunRegions, Periods/BaseYearCorineDataFileName)";
		container FactorData: using = "JrcFactorData",
 			Expr =
 				"for_each_nedv(StaticFactors/UniqueName, 'convert(JrcFactorData/'+ StaticFactors/Name + '[domain/grid_rel], float32)', domain, float32)";
		
		// ============ org land
		//attribute<ModelType> 				 OrgLanduse(domain_grid): 		Expr = "JrcFactorData/TiffData/CorineGrid/ModelLandUse";
		//attribute<Classifications/LU/Corine> CorineLandUse (domain_grid): 	Expr = "JrcFactorData/TiffData/CorineGrid/CorineLandUse";
		
		// ============ allocatable land
		container Land: Descr = "These attributes are used to describe the freeLand for allocation"
		{
			attribute<Bool> IsInRunRegion(domain_grid): Expr = 
				//"Geography/Regions/Runs/RegionGridSet!RunParameters/Name"; 
				"!IsNull(RunRegions/RasterSets/RunAreas)";
				
			attribute<bool> IsInDestClaimRegions(domain_grid): Expr = "= 'and('+ AsItemList('!IsNull(RunRegions/RasterSets/'+UniqueDestRegionRefs/Values+')') +')'";
			
			attribute<bool> Allocatable  (domain_grid): Expr = "IsInRunRegion && IsDefined(PrepareVariables/LandUseGrid) && IsInDestClaimRegions";
			
			unit<UInt32> FreeLandCells: Expr = "subset(Allocatable)", DialogType = "Map", DialogData = "BasisGrid"
			{
				attribute<FreeLandCells> permutation: Expr = "rnd_permutation(0, FreeLandCells)";
				attribute<domain_grid>   grid_rel (FreeLandCells): Expr = "lookup(permutation, nr_OrgEntity)";
				attribute<FreeLandCells> BasisGrid  (domain_grid): Expr = "invert(grid_rel)";
				parameter<uint32> 		 p99_value:				Expr = "uint32(float32(#.) * float32(0.99))";
			}
		}
		
		// ============ Regions meta
		attribute<String> RegionRefs	(ModelType): Expr = "DemandSet/RegionRefs[ModelType/DemandCharacteristicsRel]";
		attribute<String> DestRegionRefs(ModelType): Expr = "DemandSet/DestRegionRefs[ModelType/DemandCharacteristicsRel]";
		unit<UInt8> UniqueRegionRefs:     Expr = "DemandSet/RunRegion_Specific_RegionRefs";
		unit<Uint8> UniqueDestRegionRefs: Expr = "DemandSet/UniqueDestRegionRefs";
		unit<Uint8> AllRegionRefs:        Expr = "DemandSet/AllRegionRefs";
	
		unit<uint16> DestPopClaimRegion: 	Expr = "= 'RunRegions/UnitSets/'+ DestRegionRefs[ModelType/Urban]";
		unit<uint16> OrgPopClaimRegion: 	Expr = "= 'RunRegions/UnitSets/'+ RegionRefs[ModelType/Urban]";
		attribute<OrgPopClaimRegion>  OrgPopRasterLink	(domain_grid):		Expr = "= 'RunRegions/RasterSets/'+ RegionRefs[ModelType/Urban]";
		attribute<DestPopClaimRegion> DestPopRasterLink (domain_grid):		Expr = "= 'RunRegions/RasterSets/'+ DestRegionRefs[ModelType/Urban]";
		
		container DemandZoneConversions: using = "MetaData/DemandCharacteristics"
		{
			container inClaimTables: Expr = "DemandSet/ClaimTables";
			
			attribute<bool> OrgRegionIsDestinationRegion (ModelType): expr = "IsNull(RegionRefs) || RegionRefs = DestRegionRefs";
			
			container Conversions: expr = "for_each_ne(Name, OrgRegion = ModelRegion ? "
				"'SameZones(' + quote(OrgRegion) + ')' : "
				"'reaggregateWeights('+ quote(OrgRegion) +', '+ quote(ModelRegion) + (strlen(ReaggregateVar) > 0 ? ', ' + quote(ReaggregateVar) : '') + ')')", FreeData = "False"
			{
				Container OrgPopToOrgUrban: expr = "= OrgRegion[DC/Urban] = OrgRegion[DC/Population] ? "
				"'SameZones(' + quote(OrgRegion[DC/Population]) + ')' : "
				"'reaggregateWeights('+ quote(OrgRegion[DC/Population]) +', '+ quote(OrgRegion[DC/Urban]) + "
					"(strlen(ReaggregateVar[DC/Population]) > 0 ? ', ' + quote(ReaggregateVar[DC/Population]) : '') + ')'";
				
				Container OrgTouristicToModelUrban: expr = "= OrgRegion[DC/Touristic] = ModelRegion[DC/Urban] ? "
				"'SameZones(' + quote(OrgRegion[DC/Touristic]) + ')' : "
				"'reaggregateWeights('+ quote(OrgRegion[DC/Touristic]) +', '+ quote(ModelRegion[DC/Urban]) + "
					"(strlen(ReaggregateVar[DC/Touristic]) > 0 ? ', ' + quote(ReaggregateVar[DC/Touristic]) : '') + ')'";
					
				Container OrgTouristicToOrgUrban: expr = "= OrgRegion[DC/Touristic] = OrgRegion[DC/Urban] ? "
				"'SameZones(' + quote(OrgRegion[DC/Touristic]) + ')' : "
				"'reaggregateWeights('+ quote(OrgRegion[DC/Touristic]) +', '+ quote(OrgRegion[DC/Urban]) + "
					"(strlen(ReaggregateVar[DC/Touristic]) > 0 ? ', ' + quote(ReaggregateVar[DC/Touristic]) : '') + ')'";
			}
			
			container SameZones: IsTemplate = "True", Using = "RunRegions/UnitSets;"
			{
				parameter<string> OrgClaimZoneSetName;
				
				unit<uint16> crossTab: expr = "= 'RunRegions/UnitSets/' + OrgClaimZoneSetName"
				{
					attribute<crossTab> nr_1: 			expr = "id(.)";
					attribute<crossTab> nr_2: 			expr = "id(.)";
					attribute<float32> orgzone_weight: 	expr = "const(float32(1), ., float32)";
				}
			}
			
			container ReaggregateWeights: IsTemplate = "True", Using = "RunRegions/UnitSets;"
			{
				parameter<string> OrgClaimZoneSetName;
				parameter<string> DestClaimZoneSetName;
				parameter<string> agg_weight_def: 				expr = "'const(float32(1), domain, float32)'"; // expr = "'base_zones/area'";
				
				unit<uint16> OrgClaimZone: 					expr = "= 'RunRegions/UnitSets/' + OrgClaimZoneSetName";
				unit<uint16> DestClaimZone: 				expr = "= 'RunRegions/UnitSets/' + DestClaimZoneSetName";
				unit<uint32> crossTab: expr = "combine(OrgClaimZone, DestClaimZone)" 
				{
					attribute<wpoint> 	ct_id: 			expr = "point(nr_1, nr_2, wpoint)"; 
					attribute<float32>	orgzone_weight:	expr = "sum(weight, ct_ids) / sum(weight, nr_1[ct_ids])[nr_1]";
				}
				attribute<float32> 		weight		 (domain): expr = "= agg_weight_def";
				attribute<wpoint> 		ct_id_points (domain): expr = "= 'point(RunRegions/RasterSets/'+ OrgClaimZoneSetName +'[domain/grid_rel], RunRegions/RasterSets/'+ DestClaimZoneSetName +'[domain/grid_rel], wpoint)'";
				attribute<crossTab>		ct_ids		 (domain): expr = "rlookup(ct_id_points, crossTab/ct_id)";
				
				// 32 version would use upoint not wpoint!
			}
			
		}
		
		/*unit<uint32> PrepareRegionalFactors: expr = "range(uint32, 0, #AllRegionRefs * #MetaData/RegionalDynamicFactorsList)"
		{
			attribute<MetaData/RegionalDynamicFactorsList> 		nr_1: 		expr = "(id(.) / uint32(#AllRegionRefs))[MetaData/RegionalDynamicFactorsList]";
			attribute<AllRegionRefs>							nr_2: 		expr = "mod(id(.), uint32(#AllRegionRefs))[AllRegionRefs]";
			attribute<string>									zonename: 	expr = "AllRegionRefs/Values[nr_2]";
			attribute<string>									varname:	expr = "MetaData/RegionalDynamicFactorsList/Name[nr_1]";
			attribute<string>									name:		expr = "varname + '_by_' + zonename";
			attribute<string>									RegExpr: 	expr = "MetaData/RegionalDynamicFactorsList/ToAggregate[nr_1] ? "
				"MetaData/RegionalDynamicFactorsList/AggregationFunction[nr_1] + '(' + MetaData/RegionalDynamicFactorsList/RegExpr[nr_1] + '[domain/BasisGrid], RunRegions/RasterSets/'+ zonename + ')' :"
				"MetaData/RegionalDynamicFactorsList/RegExpr[nr_1]";
		}*/
		
		// ============ Regions

		// ============ claims
		container ClaimTables: Expr = "DemandSet/ClaimTables";
		attribute<String> ClaimTableName		(ModelType): Expr = "MakeDefined(DC/OrgRegion[ModelType/DemandCharacteristicsRel], 'ForestDemandRegions')";
		attribute<String> DestClaimTableName	(ModelType): Expr = "MakeDefined(DC/ModelRegion[ModelType/DemandCharacteristicsRel], ClaimTableName)";
		
		container RegionGrids: Expr = "for_each_nedvn(AllRegionRefs/Values, 'RunRegions/RasterSets/'+AllRegionRefs/Values+'[domain/grid_rel]', domain, RunRegions/UnitSets, AllRegionRefs/Values)";

		container RegionNumbersSpecTempl: Istemplate = "True"
		{
			unit<uint16> RegionsOrg;
			parameter<string> RegionsSetName;
		
			attribute<RegionsOrg> RegionMapOrg (domain): expr = "= 'RegionGrids/'+RegionsSetName";
			
			unit<UInt16> LocalRegions16: Expr = "RegionsOrg", DialogType = "Map", DialogData = "LocalRegionGrid"
			{
				attribute<.> LocalRegionGrid (domain_grid): expr = "RegionMapOrg[domain/BasisGrid]";
				attribute<string> Label: expr = "RegionsOrg/Label";
				attribute<RegionsOrg> Values: expr = "id(.)";
			}
			unit<UInt32> LocalRegions32: Expr = "Range(LocalRegions16, UInt32(0), UInt32(#LocalRegions16))", DialogType = "Map", DialogData = "LocalRegionGrid"
			{
				attribute<.> LocalRegionGrid (domain_grid): Expr = "LocalRegions16/LocalRegionGrid[.]";
				attribute<String>     Label:  Expr = "LocalRegions16/Label [value(ID(.),LocalRegions16)]", DialogType = "LabelText";
				attribute<RegionsOrg> Values: Expr = "rlookup(id(.), UInt32(id(RegionsOrg)))";
			}
		}		
		unit<UInt16> RegionNumbersTempl: Istemplate = "True";
		
		container RegionNumbersSpec: 	Expr = "for_each_ne(AllRegionRefs/Values, 'RegionNumbersSpecTempl(RunRegions/UnitSets/'+AllRegionRefs/Values+', '+quote(AllRegionRefs/Values)+')')";
		container RegionNumbers: 		Expr = "for_each_net(AllRegionRefs/Values, 'RegionNumbersSpec/'+AllRegionRefs/Values+'/LocalRegions16', RegionNumbersTempl)";
		container RegionSizes:			Expr = "for_each_nednv(AllRegionRefs/Values, 'pcount(RegionGrids/'+AllRegionRefs/Values+')', RunRegions/UnitSets, AllRegionRefs/Values, uint32)";
		
		// ============ Renumbered Regions
		container RegionData: Expr = "for_each_nedvn(AllRegionRefs/Values, 'invert(RegionNumbers/'+AllRegionRefs/Values+'/Values)[RegionGrids/'+AllRegionRefs/Values+']', domain, RegionNumbers, AllRegionRefs/Values)";
		container OrgLandUseCount: Expr = "reg_count(StartingState/LandUse[domain/BasisGrid], ModelType/Name, RunRegions/RasterSets, DestClaimTableName)"; //MakeDefined(AllRegionRefs, 'NUTS2_LUISA_old'))"; //[domain/grid_rel]
		
		container ClaimTableSelections: Expr = "for_each_ne(UniqueRegionRefs/Values, 'ClaimTableSelectionTemplate(ClaimTables/'+UniqueRegionRefs/Values+', Parameters/PeriodSet/StartYear)')";
		
		container adapt_claim: expr = "for_each_nednv(!ModelType/DynamicDemand ? ModelType/Name : '', "
					"'MakeDefined('+"
//							"IsDefined(invert(CaseData/AtomicRegions/'+ClaimTableName+')) ?"
							"'rjoin('+"
								"'RunRegions/UnitSets/'+ClaimTableName+'/ZoneId, '+"
								"'CaseData/ClaimTables/'+ClaimTableName+'/Data/RegionUp[ClaimTableSelections/'+ClaimTableName+'/sel/nr_OrgEntity], '+"
								"'float32(CaseData/ClaimTables/'+ClaimTableName+'/Data/'+MakeDefined(ModelType/MinClaimColumn, 'ZeroClaim')+'[ClaimTableSelections/'+ClaimTableName+'/sel/nr_OrgEntity])'+"
							"')'+"
						"', float32(0))'"
						", RegionNumbers, ClaimTableName, float32)", FreeData = "False";
		
		unit<uint16> AtomicRegions: Expr = "overlay(UniqueDestRegionRefs/Values, domain, RegionData)" //, DialogData = "UnionData", DialogType = "map"
		{
			attribute<AtomicRegions> UnionData(domain);
			attribute<uint32> 		 Recs: expr = "Pcount(UnionData)";
			attribute<AtomicRegions> ID: Expr = "ID(AtomicRegions)";
			attribute<bool>			 error (domain): expr = "IsNull(UnionData)";
		}
		
		// redundant with modeltraits. May need to be shifted to global templateset.
		container ClaimTableSelectionTemplate : IsTemplate = "True"
		{
			container ClaimTable;
			parameter<YearRange> FutureYear;
			unit<uint32> sel: expr = "Subset(Float32(ClaimTable/Data/Year) == FutureYear)"
			{
				attribute<String> Label: Expr = "ClaimTable/Domain/Label[nr_OrgEntity]", DialogType = "LabelText";
				
			}
		}
		
		//attribute<bool> Nuts2MShape_inRun(Geography/Regions/Nuts2_Merged_LUISA_old): expr = "!IsNull(rlookup(id(Geography/Regions/Nuts2_Merged), RegionNumbersSpec/Nuts2_Merged/LocalRegions16/Values))";
		//attribute<bool> Nuts2Shape_inRun (Geography/Regions/Nuts2_LUISA_old):		 expr = "!IsNull(rlookup(id(Geography/Regions/Nuts2), RegionNumbersSpec/Nuts2/LocalRegions16/Values))";
		//attribute<bool> Nuts3Shape_inRun (Geography/Regions/Nuts3_LUISA_old): 		 expr = "Nuts2Shape_inRun[Geography/Regions/Nuts3/Nuts2_rel]";
		//attribute<bool> LAUShape_inRun 	 (Geography/RegionShapes/composite_LAU): expr = "Nuts2Shape_inRun[Geography/RegionShapes/composite_LAU/Nuts2Id]";

		// ============ Regions
		//container StartingLuPerClaimRegion: Expr = "for_each_nednv(IsDefined(RegionRefs) ? ModelType/Name : '', 'sum(UInt32(OrgLandUse[domain/grid_rel]==Value('+String(ID(ModelType))+', ModelType)), RegionData/'+RegionRefs+')', Geography/Regions, RegionRefs, UInt32)";
		
		// ============ Suitabilites

		unit<UInt32> CurrFactorWeights: Expr = "Rules/CurrFactorWeights";
		//container WeightedSuitabilityFactorData: expr = "for_each_nedv(CurrFactorWeights/Name, Rules/SuitTermExpr, domain, Float32)";
		//attribute<Float32> SuitFactorWeights(CurrFactorWeights): expr = "='union_data(CurrFactorWeights,'+asItemList('sd(WeightedSuitabilityFactorData/'+CurrFactorWeights/Name+')')+')'";
		//container SuitabilityData:  Expr = "for_each_nedv(ModelType/Name, Rules/SuitTermsExpr, domain, Float32)";
		//container SuitabilityLogit: Expr = "for_each_nedv(ModelType/Name, Rules/SuitabilityLogitExpr, domain, Float32)";
		//attribute<Float32> TotalExpSuitability(domain): Expr = "=Rules/SuitabilitySumExpExpr";
		//container SuitabilityMNL:   Expr = "for_each_nedv(ModelType/Name, Rules/SuitabilityMNLExpr, domain, Float32)";
		
		
		// ============ locspecs
		container LocSpec:
			Expr = 
				"for_each_nedv(ModelType/Name, "
					"'float32(1) + MakeDefined(Float32(JrcFactorData/'+ParamSet/LocSpecContainerName+'/' + ModelType/Name + '[domain/grid_rel] ) / Float32(100), float32(0))',"
					"domain, Float32"
				")";
		// ============ Fixed Parameters
		parameter<Float32> SuitScaleFactor: Expr = "value(1, Meter2 / luHa)";
		unit<int32> EUR_Ha_int32: Expr = "int32(EUR_HA)";
		
		#include<getOptimalk_NUTS2.dms>
			
	}
	
	
	container input_results
	{
		// ============ factor data export tool for 
		container ExportSamples:    Expr = "ModelTraits/DataSampleGenerator/Templ(domain, domain_grid)";
	}
	container testingAllocSums                                                               //
	{                                                                                          //
		container AllocSums:    Expr = "ModelTraits/TestSumsAlloc/Templ(domain, domain_grid)";  //
	}  
	
	container StartingState
	{
		attribute<ModelType> LandUse_Grid(domain_grid): Expr = "CaseData/PrepareVariables/LandUseGrid";
		attribute<ModelType> LandUse     (domain):      Expr = "!BurntForest ? LandUse_Grid[domain/grid_rel] : ModelType/TransWoodlandShrub";
		attribute<bool>		 BurntForest (domain): 		Expr = "(LandUse_Grid[domain/grid_rel] = ModelType/ForestsMature || LandUse_Grid[domain/grid_rel] = ModelType/ForestsYoung) && float32(CaseData/JrcFactorData/BurntAreas/BurntInYear[domain/grid_rel]) < Parameters/PeriodSet/StartYear";
		attribute<bool>		 CorineBurnt (domain): 		Expr = "Classifications/LU/Corine/Burnt[CaseData/PrepareVariables/CorineGrid[domain/grid_rel]]";
		parameter<float32>   ComingPeriodLength:		Expr = "Periods/PeriodLength[uint16(0)]";
		parameter<YearRange> CurrYear:					Expr = "Periods/CurrYear[uint16(0)]";
		parameter<bool> cumulStatusFlag: Expr = "true";
		attribute<YearDiffRange> Age    (domain): Expr = "switch("
			"case(CorineBurnt, value(3, YearDiffRange)), "
			"case(BurntForest, Parameters/PeriodSet/StartYear - float32(CaseData/JrcFactorData/BurntAreas/BurntInYear[domain/grid_rel]) - float32(1)), "
			//"value(CaseData/JrcFactorData/age0[domain/grid_rel], YearDiffRange))";
			"value(CaseData/JrcFactorData/TiffData/AgeFromCLC/AgeCLC [domain/grid_rel], YearDiffRange))";
			
		//attribute<float32> Qi(domain): Expr = "float32(CaseData/getOptimalk/modelledQi)";
		attribute<float32> Qi(domain): Expr = "float32(CaseData/getOptimalk/PopulationDistribution/Qi)";
	    attribute<float32> PopDensity_km2 (domain): Expr = "MakeDefined(potential(Qi[domain/BasisGrid], Parameters/FuncKernels/Area1km2/AbsWeight)[domain/grid_rel] , 0[NrResidents])";
		attribute<float32> Si (domain): 	Expr = "Qi";
		attribute<Year>	   	S_Agei (domain): Expr = "Qi > 0[float32] ? rnd_uniform(0, domain, range(Year, 0[Year], 10[Year])): 100[Year]"; // 0 years if supply has changed in a year
		attribute<float32> Qj	(CaseData/DestPopClaimRegion): Expr = "sum(Qi[domain/basisgrid], CaseData/DestPopRasterLink)";
		//attribute<float32>	   claimdif (CaseData/DestPopClaimRegion): expr = "Qj - Parameters/PopSets/RegionalPopulation/Demands/Qj/s2011[CaseData/DestPopClaimRegion/Nr_OrgEntity]";
		attribute<float32>	   NeighboursWeight (domain): Expr = "potential(const(float32(1), domain, float32)[domain/basisgrid], Parameters/FuncKernels/QueensNeighbours/Matr)[domain/grid_rel]";
		attribute<float32> PotQi 	(domain): Expr = "MakeDefined(potential(float32(CaseData/getOptimalk/PopulationDistribution/Qi)[domain/BasisGrid], Parameters/FuncKernels/RooksNeighbours/Matr)[domain/grid_rel] / NeighboursWeight, 0[NrResidents])";
		attribute<Classifications/LU/ForestType> ForestType(domain_grid): Expr = "Classifications/LU/ForestType/Back_rel[CaseData/JrcFactorData/TiffData/CorineGrid/CorineLandUse]";
		
		container dFactors: expr = "for_each_nedv(DynamicFactors/Name, DynamicFactors/StartExpr, domain, float32)";
		
		container regionalFactors: expr = "for_each_ne(CaseData/AllRegionRefs/Values, 'rules/Templates/getRegionalVariables('+ quote(CaseData/AllRegionRefs/Values) +')')"
		{
			//attribute<float32> ClaimPop (CaseData/OrgPopClaimRegion): expr = "Sum(Qi, CaseData/OrgPopRasterLink[domain/grid_rel])";
			//attribute<float32> AllocPop	(CaseData/OrgPopClaimRegion): expr = "ClaimPop";
		
			container ClaimPop: 		expr = "for_each_nedv(MetaData/PopulationCharacteristics/Name, 'Sum(PopulationBreakdown/'+MetaData/PopulationCharacteristics/Name+', CaseData/OrgPopRasterLink[domain/grid_rel])',CaseData/OrgPopClaimRegion, float32)";
			container AllocPop:			expr = "ClaimPop";
			
		
			container conversions: expr = "CaseData/DemandZoneConversions/Conversions";
			unit<uint32> UrbanClaimZoneMatrix: expr = "CaseData/NetworkAnalysis/urbanclaimzones_x_urbanclaimzones" 
			{
				parameter<float32> 	 RelWeightStartingYear: 	Expr = "CurrYear[uint32] >= CaseData/NetworkAnalysis/LastYear ? float32(0): float32(1) - float32((CurrYear[uint32] - CaseData/NetworkAnalysis/FirstYear)/ (CaseData/NetworkAnalysis/LastYear - CaseData/NetworkAnalysis/FirstYear))";
				attribute<float32> tij: expr = "(RelWeightStartingYear * tt_06) + ((float32(1) - RelWeightStartingYear) * tt_30)";
			}
			unit<uint32> ms_rel: expr = "MetaData/RegionalFactorDefinitions/ModelSpecificFactorsList";	
			unit<uint16> UrbanZones: expr = "= 'RunRegions/UnitSets/' + MetaData/DemandCharacteristics/OrgRegion[MetaData/DemandCharacteristics/Urban]";
			attribute<float32> touristic_lu (UrbanZones): expr = "sum((CaseData/JrcFactorData/Tourism/beds / CaseData/JrcFactorData/Tourism/beds_per_ha)[Conversions/OrgTouristicToOrgUrban/crossTab/nr_1] * Conversions/OrgTouristicToOrgUrban/crossTab/orgzone_weight, Conversions/OrgTouristicToOrgUrban/crossTab/nr_2)";
			container ModelSpecificFactors: expr = "for_each_nednv(ms_rel/Name, ms_rel/RegExpr + (ms_rel/subset ? '[RunRegions/UnitSets/'+ ms_rel/TargetZones +'/nr_OrgEntity]' : ''), RunRegions/UnitSets, ms_rel/TargetZones, float32)";
		}
		container AgriculturalAbandonment
		{
				parameter<float32> PopDensityThreshold : expr = "float32(50)";
				attribute<float32> LowPopDensityAreas	(domain): expr = "PopDensity_km2 < PopDensityThreshold ? PopDensity_km2 : PopDensityThreshold";
				attribute<float32> LowPopDensity_factor (domain): expr = "Rescale(LowPopDensityAreas, float32(1), float32(0)) ";	
										
				parameter<float32> TravelTimeThreshold : expr = "float32(60)";
				attribute<float32> RemoteAreas				(domain): expr = "dFactors/TownAccess > TravelTimeThreshold ? dFactors/TownAccess : Float32(0.0)";
				attribute<float32> RemoteAreas_factor 	(domain): expr = "Rescale(RemoteAreas, float32(0), float32(1))";
		}
		//container regionalFactors: expr = "for_each_nednv(CaseData/PrepareRegionalFactors/Name, CaseData/PrepareRegionalFactors/RegExpr, RunRegions/UnitSets, CaseData/PrepareRegionalFactors/ZoneName, float32)";
		
		parameter<string> GeneratedFiles: Expr = "OutputGeneration/OutputDirName+';'",		ExplicitSuppliers = "OutputGeneration/ClippedLandUseVT;OutputGeneration/Clipped_Qi;";
		container OutputGeneration
		{
			parameter<String> OutputDirName: Expr = "simulation_results/metadata/DirName + '/'+string(Periods/CurrYear[uint16(0)])";
			
			container Indicators {
				container NetAgriAbandonment: Expr = "IndicatorData/AgriAbandonmentStartingState(domain)";
			}
			
			parameter<String> ClippedLandUseVT_FileName: Expr = "OutputDirName + '.tif'";
			parameter<String> Qi_FileName:				 Expr = "OutputDirName + '_Qi.tif'";
			
			attribute<Classifications/LU/ViewType> LandUseVT(domain_grid)
				:	Expr = "MakeDefined( Classifications/LU/ModelType/VT[ LandUse_Grid ], Classifications/LU/Corine/VT[ CaseData/JrcFactorData/TiffData/CorineGrid/CorineLandUse ])";
				
			attribute<Classifications/LU/ViewType> ClippedLandUseVT(domain_grid)
				:	Expr = "CaseData/Land/IsInRunRegion ? LandUseVT : value(0, Classifications/LU/ViewType)"
				,	StorageName = "=ClippedLandUseVT_FileName"
			{
				attribute<UInt32> PaletteData(Classifications/LU/ViewType): Expr = "Classifications/LU/ViewType/Color";
			}
			
			attribute<int32> Qi_Grid 	(domain_grid): expr = "CaseData/getOptimalk/modelledQi[domain/BasisGrid]";
			
			attribute<int32> Clipped_Qi (domain_grid)
				:   Expr = "CaseData/Land/IsInRunRegion ? Qi_Grid : 0[int32]"
				,   StorageName = "=Qi_FileName";
			
			
		}
	}

	container TimeSteps: Expr = 
			"for_each_ne("
				"Periods/Name,"
				"'DynaClueTimeStep100(value('+Periods/YearStr+', YearRange),'+Periods/IdStr+','+Periods/ClaimFractionStr+',ModelType, domain_grid, domain,CaseData/OrgPopClaimRegion,CaseData/DestPopClaimRegion,'+Periods/PrevRef+')'"
			")";
	
 	container endstate: Expr = "='TimeSteps/'+Periods/Name[max(ID(Periods))]+'/ResultingState'"
 	{
// 		parameter<YearRange> RefTime:          Expr = "PLast/ResultingState/RefTime";
//		parameter<bool>      cumulStatusFlag:  Expr = "PLast/ResultingState/cumulStatusFlag";
	
	 	attribute<ModelType> discr_result(domain): Expr  = "LandUse";
	 	attribute<ModelType> changed_to  (domain): Expr = "(discr_result != StartingState/LandUse) ? discr_result          : value(0 / 0, ModelType)";
	 	attribute<ModelType> changed_from(domain): Expr = "(discr_result != StartingState/LandUse) ? StartingState/LandUse : value(0 / 0, ModelType)";
	}
	
	container simulation_results
		:	Descr = "Allocated landuse resulting from the balancing iteration + ground-price indication"
	{
		container metadata
		{
			parameter<String> DirName: Expr = "RunParameters/OutputDir";
			
			attribute<string> AllocStatusPerPeriod(Periods): Expr = "='union_data(Periods, '+asItemList('TimeSteps/'+Periods/Name+'/d_alloc/status')+')'";
			parameter<string> AllocStatus: Expr = "asItemList(AllocStatusPerPeriod)";
		}
		container landuse: Descr = "The resulting landuse is the combination of (iterator/allocated_landuse) and (exogeousLanduse)"
		{
//			attribute<ModelType> GridData(domain_grid): StorageName = "=metadata/DirName+'.tif'", Expr = "endState/LandUse_Grid"
			attribute<Classifications/LU/ViewType> GridData(domain_grid): StorageName = "=metadata/DirName+'_VT.tif'", Expr = "=Last('TimeSteps/'+Periods/Name+'/ResultingState/OutputGeneration/ClippedLandUseVT')"
			{
				attribute<UInt32> PaletteData(Classifications/LU/ViewType): Expr = "Classifications/LU/ViewType/Color";
			}
			container PerYear:     Expr = "for_each_nedvl(Periods/Name, 'TimeSteps/'+Periods/Name+'/ResultingState/LandUse', domain, ModelType, 'Land use '+Periods/YearStr)";
			container GridPerYear: Expr = "for_each_nedvl(Periods/Name, 'TimeSteps/'+Periods/Name+'/ResultingState/LandUse_Grid', domain_grid, ModelType, 'Land use '+Periods/YearStr)";
			container ClippedGridPerYearVT_Calculated: Expr = "for_each_nedv(Periods/Name, 'TimeSteps/'+Periods/Name+'/ResultingState/OutputGeneration/ClippedLandUseVT', domain_grid, Classifications/LU/ViewType)";
			
			// decoupled simulation for mozaiking
			container ClippedGridPerYearVT_ReadAlways: Expr = "for_each_ndva(Periods/Name, domain_grid, Classifications/LU/ViewType, '=TimeSteps/'+Periods/Name+'/ResultingState/OutputGeneration/ClippedLandUseVT_FileName')";
			container ClippedGridPerYearVT_ReadOnly: Expr = 
				"for_each_nedv(Periods/Name, "
					"'=ExistingFile(''C:/@'',TimeSteps/'+Periods/Name+'/ResultingState/OutputGeneration/ClippedLandUseVT_FileName) != ''C:/@'' ? ''ClippedGridPerYearVT_ReadAlways/'+Periods/Name+''': ''const(0[Classifications/LU/ViewType],domain_grid)''',"
					"domain_grid, Classifications/LU/ViewType"
					")";
			container ClippedQiPerYearVT_ReadOnly: Expr = "for_each_ndva(Periods/Name, domain_grid, int32, '=TimeSteps/'+Periods/Name+'/ResultingState/OutputGeneration/Qi_FileName')";
			container DifQiPerYearVT_from_ReadOnly: Expr = "for_each_nedv(Periods/Name, '!IsNull(RunRegions/RasterSets/RunAreas) ? float32(ClippedQiPerYearVT_ReadOnly/' + Periods/Name + ') - CaseData/JrcFactorData/Population/Qi_2011 : float32(0)',  domain_grid, float32)";
			
		}

		container Indicators: Expr = 
			"IndicatorData/EvaluationTemplate100("
				"ModelType, Periods, "
				"domain_grid, domain,"
				"landuse/GridPerYear, landuse/PerYear, "
				"CaseData/JrcFactorData, "
				"simulation_results/metadata/DirName + '/Indicators',"
				"Parameters/PopSets/RegionalPopulation/Demands/Qj,"
				"CaseData/RegionNumbers/Nuts2, "
				"CaseData/RegionNumbers/Nuts2/LocalRegionGrid,"
				"CaseData/RegionNumbers/Nuts2/Values "
//				"CaseData/RegionData/Nuts2"
			")";

		parameter<string>  GeneratedFiles: Expr = "endState/GeneratedFiles", ExplicitSuppliers = "endState/GeneratedFiles;simulation_results/landuse/GridData";
	}
}
